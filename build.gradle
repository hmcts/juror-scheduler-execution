buildscript {
  ext {
    springBootVersion = '3.3.0'
    postgresVersion = '42.7.3'
    log4JVersion = "2.23.1"
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    classpath("org.postgresql:postgresql:${postgresVersion}")
  }
}

plugins {
  id 'application'
  id 'java-library'
  id 'maven-publish'
  id 'checkstyle'
  id 'pmd'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.1.5'
  id 'org.springframework.boot' version "${springBootVersion}"
  id 'org.owasp.dependencycheck' version '9.2.0'
  id 'com.github.ben-manes.versions' version '0.51.0'
  id 'org.sonarqube' version '5.0.0.4638'
}

group = 'uk.gov.hmcts.juror'
version = '0.0.1'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

sourceSets {
  functionalTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/functionalTest/java')
    }
    resources.srcDir file('src/functionalTest/resources')
  }

  integrationTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/integrationTest/java')
    }
    resources.srcDir file('src/integrationTest/resources')
  }

  smokeTest {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/smokeTest/java')
    }
    resources.srcDir file('src/smokeTest/resources')
  }
}

configurations {
  functionalTestImplementation.extendsFrom testImplementation
  functionalTestRuntimeOnly.extendsFrom runtimeOnly

  integrationTestImplementation.extendsFrom testImplementation
  integrationTestRuntimeOnly.extendsFrom runtimeOnly

  smokeTestImplementation.extendsFrom testImplementation
  smokeTestRuntimeOnly.extendsFrom runtimeOnly
}

tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

// https://github.com/gradle/gradle/issues/16791
tasks.withType(JavaExec).configureEach {
  javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

tasks.withType(Test) {
  useJUnitPlatform()

  testLogging {
    exceptionFormat = 'full'
  }
}

test {
  failFast = true
}

task functional(type: Test) {
  description = "Runs functional tests"
  group = "Verification"
  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath
}

task integration(type: Test) {
  description = "Runs integration tests"
  group = "Verification"
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  failFast = true
}

task smoke(type: Test) {
  description = "Runs Smoke Tests"
  testClassesDirs = sourceSets.smokeTest.output.classesDirs
  classpath = sourceSets.smokeTest.runtimeClasspath
}

checkstyle {
  maxWarnings = 0
  toolVersion = '10.17.0'
  getConfigDirectory().set(new File(rootDir, 'config/checkstyle'))
}

pmd {
  toolVersion = "6.55.0"
  sourceSets = [sourceSets.main, sourceSets.test, sourceSets.functionalTest, sourceSets.integrationTest, sourceSets.smokeTest]
  reportsDir = file("$project.buildDir/reports/pmd")
  // https://github.com/pmd/pmd/issues/876
  ruleSets = []
  ruleSetFiles = files("config/pmd/ruleset.xml")
}
pmdTest {
  maxFailures = 668 //TODO reduce
}

jacocoTestReport {
  executionData(test, integration)
  reports {
    xml.required = true
    csv.required = false
    html.required = true
  }
}

project.tasks['sonarqube'].dependsOn jacocoTestReport
project.tasks['check'].dependsOn integration

sonarqube {
  properties {
    property "sonar.projectName", "Juror :: juror-scheduler-execution"
    property "sonar.projectKey", "uk.gov.hmcts.juror:juror-scheduler-execution"
  }
}

// before committing a change, make sure task still works
dependencyUpdates {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
    def regex = /^[0-9,.v-]+$/
    return !stableKeyword && !(version ==~ regex)
  }
  rejectVersionIf { selection -> // <---- notice how the closure argument is named
    return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
  }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  // Specifies if the build should be failed if a CVSS score above a specified level is identified.
  // range of 0-10 fails the build, anything greater and it doesn't fail the build
  failBuildOnCVSS = 0
  suppressionFile = 'config/owasp/suppressions.xml'

  analyzers {
    // Disable scanning of .NET related binaries
    assemblyEnabled = false
  }
  skipConfigurations = [
    "checkstyle",
    "compileOnly",
    "pmd",
    "integrationTest",
    "functionalTest",
    "smokeTest",
    "contractTestRuntimeClasspath",
    "contractTestCompileClasspath"
  ]
}

repositories {
  mavenLocal()
  mavenCentral()
  maven { url 'https://jitpack.io' }
}

dependencies {
  implementation 'com.github.hmcts:juror-spring-support-library:1.1.2'

  implementation("org.springframework.boot:spring-boot-starter-web:${springBootVersion}")
  implementation("org.springframework.boot:spring-boot-starter-actuator:${springBootVersion}")
  implementation("org.springframework.boot:spring-boot-starter-aop:${springBootVersion}")
  implementation("org.springframework.boot:spring-boot-starter-json:${springBootVersion}")
  implementation("org.springframework.boot:spring-boot-starter-validation:${springBootVersion}")

  implementation group: 'org.springdoc', name: 'springdoc-openapi-starter-webmvc-ui', version: '2.5.0'

  implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: '6.1.5'

  implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: log4JVersion
  implementation group: 'org.apache.logging.log4j', name: 'log4j-to-slf4j', version: log4JVersion

  implementation group: 'io.rest-assured', name: 'rest-assured'
  implementation 'org.apache.httpcomponents.client5:httpclient5:5.3.1'


  implementation 'org.springframework.integration:spring-integration-sftp:6.3.1'

  // Database, repository
  implementation 'jakarta.persistence:jakarta.persistence-api:3.2.0'
  implementation group: 'org.postgresql', name: 'postgresql', version: "${postgresVersion}"
  implementation 'org.hibernate.orm:hibernate-envers:6.5.2.Final'

  // Documentation, logging
  implementation 'org.projectlombok:lombok:1.18.32'
  testImplementation 'org.projectlombok:lombok:1.18.32'
  implementation 'org.jetbrains:annotations:24.1.0'
  testImplementation 'junit:junit:4.13.2'
  annotationProcessor 'org.projectlombok:lombok:1.18.32'
  testAnnotationProcessor 'org.projectlombok:lombok:1.18.32'
  implementation 'org.springframework.data:spring-data-commons:3.3.1'
  runtimeOnly(group: 'com.microsoft.azure', name: 'applicationinsights-core', version: '3.5.3')

  // Data Mapping
  implementation 'org.modelmapper:modelmapper:3.2.0'
  implementation 'org.mapstruct:mapstruct:1.5.5.Final'
  annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'


  compileOnly 'jakarta.servlet:jakarta.servlet-api:6.1.0'


  // Security
  implementation "org.springframework.boot:spring-boot-starter-security"
  implementation 'io.jsonwebtoken:jjwt-api:0.12.5'
  runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.5'
  runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.5'


  // Web requests
  implementation 'org.springframework:spring-webflux:6.1.9'
  implementation 'org.apache.httpcomponents.client5:httpclient5:5.3.1'

  testImplementation(platform('org.junit:junit-bom:5.10.2'))
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', {
    exclude group: 'junit', module: 'junit'
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
  }
}
mainClassName = 'uk.gov.hmcts.juror.job.execution.Application'

bootJar {
  archiveFileName = "api-job-execution-service.jar"

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

// Gradle 7.x issue, workaround from: https://github.com/gradle/gradle/issues/17236#issuecomment-894768083
rootProject.tasks.named("processSmokeTestResources") {
  duplicatesStrategy = 'include'
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}
task runAllStyleChecks {
  dependsOn 'checkstyleMain'
  dependsOn 'checkstyleTest'
  dependsOn 'checkstyleIntegrationTest'
  dependsOn 'checkstyleSmokeTest'
  dependsOn 'checkstyleFunctionalTest'

  dependsOn 'pmdMain'
  dependsOn 'pmdTest'
  dependsOn 'pmdIntegrationTest'
  dependsOn 'pmdSmokeTest'
  dependsOn 'pmdFunctionalTest'
}